plugins {
    // micronaut
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id "io.micronaut.application" version "1.5.2"

    // akhq
    id "com.gorylenko.gradle-git-properties" version "2.3.1"
    id 'com.adarshr.test-logger' version '3.0.0'
    id 'org.gradle.test-retry' version '1.2.1'
    id 'com.github.psxpaul.execfork' version '0.1.15'
    id "com.github.ben-manes.versions" version "0.39.0"
    id "com.github.davidmc24.gradle.plugin.avro" version "1.2.0"
    id "com.palantir.git-version" version "0.12.3"
}

group "org.akhq"
// Grab version from last Git tag with git-version plugin
def gitVersionDetails = versionDetails()
version gitVersionDetails.lastTag
mainClassName = "org.akhq.App"
sourceCompatibility = 11

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://packages.confluent.io/maven/' }
    maven { url 'https://jitpack.io' }
}


configurations.all {
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
    exclude group: 'org.apache.logging.log4j', module: 'log4j-core'
    exclude group: 'org.apache.logging.log4j', module: 'log4j-api'

    resolutionStrategy {
        force("org.apache.kafka:kafka-clients:" + kafkaVersion)
        force("org.apache.kafka:kafka-streams:" + kafkaVersion)
        force("org.apache.kafka:kafka_" + kafkaScalaVersion + ":" + kafkaVersion)
    }
}

/**********************************************************************************************************************\
 * Micronaut
 **********************************************************************************************************************/
micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("org.akhq.*")
    }
}

/**********************************************************************************************************************\
 * Dependencies
 **********************************************************************************************************************/
dependencies {
    // lombok
    annotationProcessor "org.projectlombok:lombok:" + lombokVersion
    compileOnly 'org.projectlombok:lombok:' + lombokVersion

    // micronaut
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-validation"
    annotationProcessor 'io.micronaut.openapi:micronaut-openapi'
    implementation "io.micronaut:micronaut-inject"
    implementation "io.micronaut:micronaut-validation"
    implementation "io.micronaut:micronaut-runtime"
    implementation "io.micronaut:micronaut-http-client"
    implementation "io.micronaut:micronaut-http-server-netty"
    implementation "io.swagger.core.v3:swagger-annotations"
    runtimeOnly 'ch.qos.logback:logback-classic:1.3.0-alpha5'
    implementation "io.micronaut.security:micronaut-security-annotations"
    implementation "io.micronaut.security:micronaut-security-jwt"
    implementation "io.micronaut.security:micronaut-security-ldap"
    implementation "io.micronaut.security:micronaut-security-oauth2"
    implementation "io.micronaut:micronaut-management"
    implementation 'io.micronaut.micrometer:micronaut-micrometer-registry-prometheus'
    implementation "io.micronaut.cache:micronaut-cache-core"

    // kafka
    implementation group: "org.apache.kafka", name: "kafka-clients", version: kafkaVersion
    implementation group: "io.confluent", name: "kafka-schema-registry-client", version: confluentVersion
    implementation group: "io.confluent", name: "kafka-avro-serializer", version: confluentVersion
    implementation group: "io.confluent", name: "kafka-json-schema-serializer", version: confluentVersion
    implementation group: "io.confluent", name: "kafka-protobuf-serializer", version: confluentVersion
    implementation 'org.sourcelab:kafka-connect-client:3.1.1'

    // strimzi
    implementation group: 'io.strimzi', name: 'kafka-oauth-common', version: '0.8.1'
    implementation group: 'io.strimzi', name: 'kafka-oauth-client', version: '0.8.1'

    // log
    implementation group: 'org.slf4j', name: 'jul-to-slf4j', version: '1.8.+'
    implementation group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.8.+'

    // utils
    implementation group: 'org.codehaus.httpcache4j.uribuilder', name: 'uribuilder', version: '2.0.0'
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.12.3'

    // avro
    implementation 'org.apache.avro:avro:1.10.2'


    // protobuf
    implementation group: "com.google.protobuf", name: "protobuf-java", version: '3.17.3'
    implementation group: "com.google.protobuf", name: "protobuf-java-util", version: '3.17.3'

    // Password hashing
    implementation group: "org.mindrot", name: "jbcrypt", version: "0.4"

    // https://mvnrepository.com/artifact/org.codehaus.groovy/groovy-all
    implementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '3.0.8'
    // api

    // client
    implementation project(":client")
}

/**********************************************************************************************************************\
 * Test
 **********************************************************************************************************************/
test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }

    retry {
        maxRetries = 5
        maxFailures = 15
    }
}

dependencies {
    testImplementation 'ch.qos.logback:logback-classic:1.3.0-alpha5'

    // micronaut test
    testAnnotationProcessor "org.projectlombok:lombok:" + lombokVersion
    testCompileOnly 'org.projectlombok:lombok:' + lombokVersion

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "io.micronaut.test:micronaut-test-junit5"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.junit.jupiter:junit-jupiter-params"

    // test
    testImplementation "org.apache.kafka:kafka-clients:" + kafkaVersion + ":test"
    testImplementation "org.apache.kafka:kafka_" + kafkaScalaVersion + ":" + kafkaVersion
    testImplementation "org.apache.kafka:kafka_" + kafkaScalaVersion + ":" + kafkaVersion + ":test"
    testImplementation "io.confluent:kafka-schema-registry:" + confluentVersion
    testImplementation "io.confluent:kafka-schema-registry:" + confluentVersion + ":tests"
    testImplementation "org.apache.kafka:connect-runtime:" + kafkaVersion
    testImplementation "org.apache.kafka:connect-file:" + kafkaVersion

    testImplementation group: 'org.apache.kafka', name: 'kafka-streams', version: kafkaVersion
    testImplementation group: "io.confluent", name: "kafka-streams-avro-serde", version: confluentVersion
    testImplementation "io.confluent:kafka-connect-avro-converter:" + confluentVersion
    testImplementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.11.1'
    testImplementation 'com.salesforce.kafka.test:kafka-junit5:3.2.2'
    testImplementation 'com.fasterxml.jackson.core:jackson-core:2.12.3'
    testImplementation 'com.fasterxml.jackson.core:jackson-annotations:2.12.3'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.12.3'
    testImplementation 'org.codehaus.jackson:jackson-mapper-lgpl:1.9.13'
}

testlogger {
    theme 'mocha'
    showExceptions true
    showFullStackTraces true
    showCauses true
    slowThreshold 2000
    showStandardStreams true
    showPassedStandardStreams false
    showSkippedStandardStreams true
}

task testKafkaCluster(type:JavaExec) {
    group = 'verification'
    description = 'Start a standalone test Kafka cluster'
    classpath sourceSets.test.runtimeClasspath
    main = "org.akhq.KafkaTestCluster"
}

task testInjectData(type:JavaExec) {
    group = 'verification'
    description = 'Inject data in a existing kafka cluster'
    classpath sourceSets.test.runtimeClasspath
    main = "org.akhq.KafkaTestCluster"
    args 'inject'
}

test.dependsOn "startTestKafkaCluster"

task startTestKafkaCluster(type: com.github.psxpaul.task.JavaExecFork, dependsOn: 'testClasses') {
    group = 'verification'
    description = 'Start a global standalone test Kafka cluster during tests'
    classpath = sourceSets.test.runtimeClasspath
    main = 'org.akhq.KafkaTestCluster'
    waitForOutput = 'Test data injected sleep done'
    timeout = 300
}

/**********************************************************************************************************************\
 * Jar
 **********************************************************************************************************************/
shadowJar {
    mergeServiceFiles()
}

processResources.dependsOn ":client:installFrontend"
processResources.dependsOn ":client:assembleFrontend"
processResources.dependsOn ":client:copyClientResources"

