
buildscript {
    ext {
        joobyVersion = "1.5.+"
        confluentVersion = "5.1.0"
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "com.google.gradle:osdetector-gradle-plugin:1.4.0"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE"
        classpath "org.jooby:jooby-gradle-plugin:$joobyVersion"
    }
}

plugins {
    id "com.moowork.node" version "1.2.0"
    id 'io.franzbecker.gradle-lombok' version '1.14'
}

apply plugin: "io.spring.dependency-management"
apply plugin: "com.google.osdetector"
apply plugin: "application"
apply plugin: "jooby"
apply plugin: "idea"

group "org.kafkahq"
version "0.1"
mainClassName = "org.kafkahq.App"
sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'http://packages.confluent.io/maven/' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

lombok {
    version = '1.18.4'
    sha256 = ""
}

idea {
    module {
        downloadJavadoc = false
        downloadSources = true
    }
}

configurations {
    implementation {
        exclude  group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude  group: 'log4j', module: 'log4j'
    }
}

/**********************************************************************************************************************\
 * Jooby
 **********************************************************************************************************************/

dependencyManagement {
    imports {
        mavenBom "org.jooby:jooby-bom:$joobyVersion"
    }
}

joobyRun {
    includes = ["**/*.class", "**/*.conf", "**/*.properties", "**/*.ftl", "**/*.xml"]
}

sourceSets.main.resources {
    srcDirs = ["conf", "public"]
}

/**********************************************************************************************************************\
 * Dependencies
 **********************************************************************************************************************/
dependencies {
    // jooby
    compile "org.jooby:jooby-netty"
    compile "io.netty:netty-transport-native-epoll:${dependencyManagement.importedProperties["netty.version"]}:${osdetector.classifier.contains("linux") ? "linux-x86_64" : ""}"
    compile "io.netty:netty-tcnative-boringssl-static:${dependencyManagement.importedProperties["boringssl.version"]}"
    compile group: "org.jooby", name: "jooby-assets", version: joobyVersion
    compile group: "org.jooby", name: "jooby-livereload", version: joobyVersion
    compile group: "org.jooby", name: "jooby-jackson", version: joobyVersion
    compile group: "org.jooby", name: "jooby-ftl", version: joobyVersion
    compile group: "org.jooby", name: "jooby-whoops", version: joobyVersion

    // kafka
    compile group: "org.apache.kafka", name: "kafka-clients", version: "2.1.+"
    compile group: "io.confluent", name: "kafka-schema-registry-client", version: confluentVersion

    // utils
    compile group: 'org.codehaus.httpcache4j.uribuilder', name: 'uribuilder', version: '2.0.0'
    compile group: 'com.google.guava', name: 'guava', version: '26.0-jre'

    // debug
    compile group: "com.google.code.gson", name: "gson", version: "2.8.+"
    compileOnly group: "org.projectlombok", name: "lombok", version: "1.18.+"

    // test
    testCompile "junit:junit:4.12"
    testCompile "com.salesforce.kafka.test:kafka-junit4:3.1.0"
    testCompile "org.apache.kafka:kafka_2.12:2.1.0"
    testCompile "io.rest-assured:rest-assured:3.1.0"
    testCompile "io.confluent:kafka-schema-registry:" + confluentVersion
    testCompile "io.confluent:kafka-schema-registry:" + confluentVersion + ":tests"
}

/**********************************************************************************************************************\
 * Test
 **********************************************************************************************************************/
test {
    testLogging {
        exceptionFormat = 'full'
    }
}

/**********************************************************************************************************************\
 * Node + Webpack
 **********************************************************************************************************************/
import com.moowork.gradle.node.task.NodeTask

task webpack(type: NodeTask, dependsOn: "npmInstall") {
    script = project.file("node_modules/.bin/webpack")
    outputs.dir("public/static")
    args = ["-p", "--progress", "--mode production"]
}

jar.dependsOn "webpack"
processResources.dependsOn 'webpack'

clean.delete << file("public/static")

/**********************************************************************************************************************\
 * Jar
 **********************************************************************************************************************/
jar {
    manifest {
        attributes "Main-Class": mainClassName
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}