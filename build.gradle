plugins {
    // micronaut
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "io.micronaut.application" version "3.6.3"

    // akhq
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
    id 'com.adarshr.test-logger' version '3.1.0'
    id 'org.gradle.test-retry' version '1.4.1'
    id 'com.github.psxpaul.execfork' version '0.2.1'
    id "com.github.ben-manes.versions" version "0.43.0"
    id "com.github.davidmc24.gradle.plugin.avro" version "1.5.0"
}

group "org.akhq"
version "0.22.0"
mainClassName = "org.akhq.App"
sourceCompatibility = 11

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://packages.confluent.io/maven/' }
    maven { url 'https://jitpack.io' }
}


configurations.all {
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
    exclude group: 'org.apache.logging.log4j', module: 'log4j-core'
    exclude group: 'org.apache.logging.log4j', module: 'log4j-api'

    resolutionStrategy {
        force("org.apache.kafka:kafka-clients:" + kafkaVersion)
        force("org.apache.kafka:kafka-streams:" + kafkaVersion)
        force("org.apache.kafka:kafka-server-common:" + kafkaVersion)
        force("org.apache.kafka:kafka_" + kafkaScalaVersion + ":" + kafkaVersion)

        force("org.slf4j:slf4j-api:1.7.32")
        force("org.slf4j:jul-to-slf4j:1.7.32")
        force("org.slf4j:log4j-over-slf4j:1.7.32")
        force("com.google.protobuf:protobuf-java:3.20.3")
    }
}

/**********************************************************************************************************************\
 * Micronaut
 **********************************************************************************************************************/
micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("org.akhq.*")
    }
}

/**********************************************************************************************************************\
 * Dependencies
 **********************************************************************************************************************/
dependencies {
    // lombok
    annotationProcessor "org.projectlombok:lombok:" + lombokVersion
    compileOnly 'org.projectlombok:lombok:' + lombokVersion

    // micronaut
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-validation"
    annotationProcessor 'io.micronaut.openapi:micronaut-openapi'
    implementation "io.micronaut:micronaut-inject"
    implementation "io.micronaut:micronaut-validation"
    implementation "io.micronaut:micronaut-runtime"
    implementation "io.micronaut:micronaut-http-server-netty"
    implementation "io.swagger.core.v3:swagger-annotations"
    runtimeOnly 'ch.qos.logback:logback-classic'
    implementation "io.micronaut.security:micronaut-security-annotations"
    implementation "io.micronaut.security:micronaut-security-jwt"
    implementation "io.micronaut.security:micronaut-security-ldap"
    implementation "io.micronaut.security:micronaut-security-oauth2"
    implementation "io.micronaut:micronaut-management"
    implementation 'io.micronaut.micrometer:micronaut-micrometer-registry-prometheus'
    implementation "io.micronaut.cache:micronaut-cache-core"

    implementation "io.micronaut.rxjava2:micronaut-rxjava2"
    implementation "io.micronaut.rxjava2:micronaut-rxjava2-http-server-netty"

    // kafka
    implementation group: "org.apache.kafka", name: "kafka_" + kafkaScalaVersion, version: kafkaVersion
    implementation group: "org.apache.kafka", name: "kafka-clients", version: kafkaVersion
    implementation group: "io.confluent", name: "kafka-schema-registry-client", version: confluentVersion
    implementation group: "io.confluent", name: "kafka-avro-serializer", version: confluentVersion
    implementation group: "io.confluent", name: "kafka-json-schema-serializer", version: confluentVersion
    implementation group: "io.confluent", name: "kafka-protobuf-serializer", version: confluentVersion
    implementation group: "io.confluent", name: "kafka-client-plugins", version: confluentVersion + "-ce"
    implementation 'org.sourcelab:kafka-connect-client:4.0.2'

    // strimzi
    implementation group: 'io.strimzi', name: 'kafka-oauth-common', version: '0.11.0'
    implementation group: 'io.strimzi', name: 'kafka-oauth-client', version: '0.11.0'

    // log
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.0-alpha5'
    implementation group: 'org.slf4j', name: 'jul-to-slf4j', version: '2.0.0-alpha5'
    implementation group: 'org.slf4j', name: 'log4j-over-slf4j', version: '2.0.0-alpha5'

    // utils
    implementation group: 'org.codehaus.httpcache4j.uribuilder', name: 'uribuilder', version: '2.0.0'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'

    // avro
    implementation 'org.apache.avro:avro:1.11.1'

    // jackson-module-scala
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-scala_2.13', version: '2.13.4'

    // protobuf
    implementation group: "com.google.protobuf", name: "protobuf-java", version: '3.21.9'
    implementation group: "com.google.protobuf", name: "protobuf-java-util", version: '3.21.9'

    // Password hashing
    implementation group: "org.mindrot", name: "jbcrypt", version: "0.4"

    // https://mvnrepository.com/artifact/org.codehaus.groovy/groovy-all
    implementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '3.0.13'
    // api

    // client
    implementation project(":client")

    //AWS MSK IAM Auth
    implementation group: 'software.amazon.msk', name: 'aws-msk-iam-auth', version: '1.1.5'
}

/**********************************************************************************************************************\
 * Test
 **********************************************************************************************************************/
test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }

    retry {
        maxRetries = 5
        maxFailures = 15
    }
}

dependencies {
    testImplementation 'ch.qos.logback:logback-classic:1.2.9'

    // micronaut test
    testAnnotationProcessor "org.projectlombok:lombok:" + lombokVersion
    testCompileOnly 'org.projectlombok:lombok:' + lombokVersion

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testImplementation "io.micronaut.test:micronaut-test-junit5"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testImplementation "io.micronaut:micronaut-http-client"
    testImplementation "io.micronaut.rxjava2:micronaut-rxjava2-http-client"

    // test
    testImplementation "org.apache.kafka:kafka-clients:" + kafkaVersion + ":test"
    testImplementation "org.apache.kafka:kafka_" + kafkaScalaVersion + ":" + kafkaVersion
    testImplementation "org.apache.kafka:kafka_" + kafkaScalaVersion + ":" + kafkaVersion + ":test"
    testImplementation "io.confluent:kafka-schema-registry:" + confluentVersion
    testImplementation "io.confluent:kafka-schema-registry:" + confluentVersion + ":tests"
    testImplementation "org.apache.kafka:connect-runtime:" + kafkaVersion
    testImplementation "org.apache.kafka:connect-file:" + kafkaVersion

    testImplementation group: 'org.apache.kafka', name: 'kafka-streams', version: kafkaVersion
    testImplementation group: "io.confluent", name: "kafka-streams-avro-serde", version: confluentVersion
    testImplementation "io.confluent:kafka-connect-avro-converter:" + confluentVersion
    testImplementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.8.1'
    testImplementation 'com.salesforce.kafka.test:kafka-junit5:3.2.4'
    testImplementation 'com.fasterxml.jackson.core:jackson-core:'
    testImplementation 'com.fasterxml.jackson.core:jackson-annotations'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation 'org.codehaus.jackson:jackson-mapper-lgpl:1.9.11'
}

testlogger {
    theme 'mocha'
    showExceptions true
    showFullStackTraces true
    showCauses true
    slowThreshold 2000
    showStandardStreams true
    showPassedStandardStreams false
    showSkippedStandardStreams true
}

task testKafkaCluster(type:JavaExec) {
    group = 'verification'
    description = 'Start a standalone test Kafka cluster'
    classpath sourceSets.test.runtimeClasspath
    main = "org.akhq.KafkaTestCluster"
}

task testInjectData(type:JavaExec) {
    group = 'verification'
    description = 'Inject data in a existing kafka cluster'
    classpath sourceSets.test.runtimeClasspath
    main = "org.akhq.KafkaTestCluster"
    args 'inject'
}

test.dependsOn "startTestKafkaCluster"

task startTestKafkaCluster(type: com.github.psxpaul.task.JavaExecFork, dependsOn: 'testClasses') {
    group = 'verification'
    description = 'Start a global standalone test Kafka cluster during tests'
    classpath = sourceSets.test.runtimeClasspath
    main = 'org.akhq.KafkaTestCluster'
    waitForOutput = 'Test data injected sleep done'
    timeout = 300
}

/**********************************************************************************************************************\
 * Jar
 **********************************************************************************************************************/
shadowJar {
    mergeServiceFiles()
    zip64 true
}

processResources.dependsOn ":client:installFrontend"
processResources.dependsOn ":client:assembleFrontend"
processResources.dependsOn ":client:copyClientResources"

